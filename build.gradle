buildscript {
    repositories {
        mavenLocal()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.12.0"
    }
}

apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'project-report'
apply plugin: 'build-dashboard'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'com.gradle.plugin-publish'

ext {
    pluginVersion = '0.1.0'
    spockCoreVersion = '1.1-groovy-2.4'
    groovyYamlVersion = '3.0.10'
    groovyWsLiteVersion = '1.1.3'
    commonsLang3Version = '3.12.0'
    jacksonVersion = '2.13.2.2'
}

group 'io.dotinc'
version "${pluginVersion}"

repositories {
    mavenLocal()
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

dependencies {
    implementation(
            localGroovy(),
            gradleApi(),
            "org.codehaus.groovy:groovy-yaml:$groovyYamlVersion",
            "com.github.groovy-wslite:groovy-wslite:$groovyWsLiteVersion",
            "org.apache.commons:commons-lang3:$commonsLang3Version",
            "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    )

    testImplementation gradleTestKit()
    testImplementation("org.spockframework:spock-core:$spockCoreVersion") {
        exclude module: 'groovy-all'
    }
}

publishing {
    publications {
        pluginPublication (MavenPublication) {
            groupId "$group"
            artifactId 'gitleaks-gradle'
            version "$version"
            from components.java
            pom {
                name = 'gitleaks-gradle'
                description = 'GitLeaks gradle plugin is an ' +
                        'analysis tool based on https://github.com/zricethezav/gitleaks used to find secrets pushed into the SCM repository.'

                url = 'https://github.com/bulivlad/gitleaks-gradle'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://github.com/bulivlad/gitleaks-gradle/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'bulivlad'
                        name = 'Vlad Bulimac'
                        email = 'buli.vlad@gmail.com'
                    }
                }
                scm {
                    url = 'https://github.com/bulivlad/gitleaks-gradle'
                    connection = 'scm:git:https://github.com/bulivlad/gitleaks-gradle.git'
                    developerConnection = 'scm:git:https://github.com/bulivlad/gitleaks-gradle.git'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.pluginPublication
}

pluginBundle {
    website = 'https://github.com/bulivlad/gitleaks-gradle#readme'
    vcsUrl = 'https://github.com/bulivlad/gitleaks-gradle'
    tags = ['git-leaks', 'gitleaks', 'gradle-plugin', 'leaks-detection', 'security']

    plugins {
        gitLeaks {
            id = 'io.dotinc.gitleaks'
            displayName = 'gitleaks-gradle plugin'
            description = 'GitLeaks gradle plugin is an analysis tool based on https://github.com/zricethezav/gitleaks used to find secrets pushed into the SCM repository.'
        }
    }
}

publish.dependsOn publishPlugins

defaultTasks 'build'